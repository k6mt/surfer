plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.k6m'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-aop:3.4.4'

    implementation 'org.springframework.boot:spring-boot-starter-web:3.4.4'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.4.4'
    implementation 'org.reflections:reflections:0.10.2'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.19.0'

    implementation 'com.opencsv:opencsv:5.9'
}

//build for frontend
def frontendDir = "$projectDir/frontend/surfer"

sourceSets {
    main {
        resources {
            srcDirs = ["$projectDir/src/main/resources"]
        }
    }
}

processResources { dependsOn "copyBuildFiles" }

tasks.register("deleteCopiesFile", Delete) {
    delete file("$projectDir/src/main/resources/META-INF/resources/k6m-surfer")
}

tasks.register('installReact', Exec){
    workingDir("$frontendDir")
    inputs.dir("$frontendDir")
    group = BasePlugin.BUILD_GROUP
    if(System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')){
        commandLine "npm.cmd", "audit", "fix"
        commandLine 'npm.cmd', 'install'
    } else {
        commandLine "npm", "audit", "fix"
        commandLine "npm", "install"
    }
}

tasks.register("buildReact", Exec){
    dependsOn("installReact")
    workingDir("$frontendDir")
    inputs.dir("$frontendDir")
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine "npm.cmd", "run-script", "build"
    } else {
        commandLine "npm", "run-script", "build"
    }
}

tasks.register("copyBuildFiles", Copy) {
    dependsOn("deleteCopiesFile", "buildReact")
    from("$frontendDir/dist")
    into("$projectDir/src/main/resources/META-INF/resources/k6m-surfer")
}


